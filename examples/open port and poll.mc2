// @endpoint http://localhost:8080/api/v0/exec

BACnetIsOpened ! // if not already opened...
<% 
  {
    'port' '/dev/ttyUSB1'
    'baudrate' 19200
    'databits' 8
    'stopbits' 1
    'parity' 'none'
  } BACnetOpenLocalDevice   // do not returns anything. Only one MS/TP network is supported. 
                            // Feel free to contribute for multiple ports or IP :)
%> IFT

// note : if the port is unplugged, it will be detected and safely closed within 10 seconds
// by the plugin.

// open device #620001  with a 15000 millisecond timeout
620001 15000 BACnetOpenRemoteDevice  'remoteDevice' STORE
// the first call to BACnetOpenRemoteDevice is long and may fail. 
// following calls to BACnetOpenRemoteDevice use a cache.

true 'showAllBacnetProperties' STORE

$remoteDevice
{
  "CurrentTemperatureZ1"   "analogValue" 101 BACnetBuildObjectId
  "CurrentSetPointZ1"      "analogValue" 102 BACnetBuildObjectId
  "CurrentTemperatureZ2"   "analogValue" 201 BACnetBuildObjectId
  "CurrentSetPointZ2"      "analogValue" 202 BACnetBuildObjectId
} $showAllBacnetProperties  BACnetReadObjects
// will return a map with present-value and interesting properties for each object.


STOP 


// after this first read, bench the read speed
1 100 
<%
  DROP
  'poll' CHRONOSTART
  $remoteDevice
  {
  "V101"   "analogValue" 101 BACnetBuildObjectId
  "V102"   "analogValue" 102 BACnetBuildObjectId
  "V105"   "analogValue" 105 BACnetBuildObjectId
  "V201"   "analogValue" 201 BACnetBuildObjectId
  "V202"   "analogValue" 202 BACnetBuildObjectId
  "V205"   "analogValue" 205 BACnetBuildObjectId 
  } BACnetReadObjects  // when boolean is absent, the default is to read present-value only.

  'poll' CHRONOEND
%> FOR

CHRONOSTATS
// total time 16506456955 ns 
// 16506456955 ns 100 / HUMANDURATION  ~= 160 milliseconde par appels, quand appels successifs.

